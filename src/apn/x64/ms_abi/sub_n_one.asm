
;   O---------------------------------------------------------------------------O
;   |                                                                           |
;   |                   SUB SINGLE-LIMB FROM APN-ARR FUNCTIONS                  |
;   |                                                                           |
;   O---------------------------------------------------------------------------O

.code

	option casemap:none

    ;   Function Arguments
    ;
    ;   rcx -> result (apn_seg*)   
    ;   rdx -> op1 (const apn_seg*)
    ;   r8  -> size (apn_size)
    ;   r9  -> val (apn_seg)

sub_n_one_zen4 PROC FRAME
.endprolog

    ; assumes r8 is at least 1
    ; via APAC_ASSERT in caller function

    mov     rax, QWORD PTR [rdx]
    sub     rax, r9
    mov     QWORD PTR [rcx], rax

    lea     rdx, [rdx + 8]
    lea     rcx, [rcx + 8]
    setc    al
    dec     r8

    mov     r11, r8
    shr     r8,  2
    and     r11, 3
    bt      ax,  0          ; doesn't modify zero flag
    jz      before_unroll

small_loop:

    mov     rax, QWORD PTR [rdx]
    sbb     rax, 0
    mov     QWORD PTR [rcx], rax

    lea     rdx, [rdx + 8]
    lea     rcx, [rcx + 8]
    dec     r11
    jnz     small_loop

before_unroll:

    setc    al
    test    r8,  r8
    bt      ax,  0      ; same here       
    jz      end_of_func

loop_unrolled:

    mov     rax, QWORD PTR [rdx]
    sbb     rax, 0
    mov     QWORD PTR [rcx], rax

    mov     rax, QWORD PTR [rdx + 8]
    sbb     rax, 0
    mov     QWORD PTR [rcx + 8], rax

    mov     rax, QWORD PTR [rdx + 16]
    sbb     rax, 0
    mov     QWORD PTR [rcx + 16], rax

    mov     rax, QWORD PTR [rdx + 24]
    sbb     rax, 0
    mov     QWORD PTR [rcx + 24], rax

    lea     rdx, [rdx + 32]
    lea     rcx, [rcx + 32]
    dec     r8
    jnz     loop_unrolled

end_of_func:

    setc    al
    movzx   rax, al
    ret

sub_n_one_zen4 ENDP


; Generic x64 routine

sub_n_one_x64 PROC FRAME
.endprolog

    mov     rax, QWORD PTR [rdx]
    sub     rax, r9
    mov     QWORD PTR [rcx], rax

    lea     rdx, [rdx + 8]
    lea     rcx, [rcx + 8]
    dec     r8
    jz      end_of_func

main_loop:

    mov     rax, QWORD PTR [rdx]
    sbb     rax, 0
    mov     QWORD PTR [rcx], rax

    lea     rdx, [rdx + 8]
    lea     rcx, [rcx + 8]
    dec     r8
    jnz     main_loop

end_of_func:

    setc    al
    movzx   rax, al
    ret

sub_n_one_x64 ENDP

END